! =================================================!
! ANSYS APDL-scripts - Verification
!    -----------------------------------------
!       Geometry: [m]
!       Force:    [N]
!       Moment:   [Nm]
!       Pressure: [Pa]
! =================================================!

FINISH		 ! Make sure current ANSYS module is closed
/CLEAR,START ! Clear the ANSYS workspace 

! White background
/RGB,INDEX,100,100,100, 0   
/RGB,INDEX, 80, 80, 80,13   
/RGB,INDEX, 60, 60, 60,14   
/RGB,INDEX, 0, 0, 0,15  

! =================================================!
! INPUT DATA - including material
! =================================================!

PI = 4*ATAN(1.0) ! Define variable PI as 3.1415...

! Remember to update title
*dim,TITLE_TEXT,string,20 ! Maximum 20 characters
/TITLE,%TITLE_TEXT(1)% ADPL verification of simple bone

!Young and poission
YOUNG_COMPACT = 19e9
POISSON_COMPACT = 0.4

!Dimensions
TH=0.035
!Max stress on beam
M=30.34963218
I=(1/12)*TH**4
h=0.0175
sigmamax=(M/I)*(TH/2)

! =================================================!
! GEOMETRY DEFINITIONS - in preprocessor (/PREP7)
! =================================================!
/PREP7 ! Enter the preprocessor

!Keypoints
K,1,0,0,0
K,2,0.035,0,0
K,3,0.035,0.4225/2,0
K,4,0.035,0.4225,0
K,5,0,0.4225,0
K,6,0,0.4225/2,0

!Lines
L,1,2
L,2,3
L,3,6
L,6,1
L,3,4
L,4,5
L,5,6

!Area from lines
AL,1,2,3,4
AL,3,5,6,7

! =================================================!
! MATERIAL DEFINITIONS
! =================================================! 

MP,EX,1,YOUNG_COMPACT
MP,PRXY,1,POISSON_COMPACT

! =================================================!
! ELEMENT DEFINITIONS AND MESHING
! =================================================!
!Initialize mesh size
meshx=0.004

*ASK,TYPE,Select elementtype: 1=>Plane182(Q4 or CST) 2=>Plane182(Q6) 3=>Plane183(Q8 or LST)
!For CST, Q4
*IF,TYPE,EQ,1,THEN
ET,1,PLANE182
KEYOPT,1,3,3
R,1,TH,

!For Q6
*ELSEIF,TYPE,EQ,2,THEN
ET,1,PLANE182
KEYOPT,1,1,2
KEYOPT,1,3,3
R,1,TH,

!For LST, Q8
*ELSEIF,TYPE,EQ,3,THEN
ET,1,PLANE183
KEYOPT,1,3,3
R,1,TH,
*ENDIF

*ASK,Shape,Select triangular or quadrilateral: 1=>Triangular (CST or LST) 2=>Quadrilateral (Q4, Q6 or Q8)
!Meshtool
FLST,5,7,4,ORDE,2
FITEM,5,1
FITEM,5,-7
CM,_Y,LINE
LSEL, , , ,P51X
CM,_Y1,LINE
CMSEL,,_Y
LESIZE,_Y1,meshx, , , , , , ,1
!Selecting shape of element
*IF,Shape,EQ,1,THEN
MSHAPE,1,2D
*ELSEIF,Shape,EQ,2,THEN
MSHAPE,0,2D
*ENDIF
MSHKEY,1
FLST,5,2,5,ORDE,2
FITEM,5,1
FITEM,5,-2
CM,_Y,AREA
ASEL, , , ,P51X
CM,_Y1,AREA
CHKMSH,'AREA'
CMSEL,S,_Y
AMESH,_Y1
CMDELE,_Y
CMDELE,_Y1
CMDELE,_Y2

! =================================================!
! DEFINE BOUNDARY CONDITIONS AND LOADS
! =================================================! 

!Clamped
NSEL,S,LOC,Y,0
D,ALL,,,,,,ALL
NSEL,ALL

!Force
NSEL,S,LOC,Y,0.4225
NSEL,R,LOC,X,0.0175
F,ALL,FX,72.66
NSEL,ALL

!Moments pressure
SFL,6,PRES,-sigmamax,sigmamax

FINISH! Exit the preprocessor

! =================================================!
! SOLUTION - in solution (/SOL)
! =================================================!

/SOL	! Enter the solution module
SOLVE	! Obtain solution

FINISH	! Exit the solution module

! =================================================!
! STUDY RESULTS - in postprocessor (/POST1) 
! =================================================!

!Plane Stress plot in x direction
/POST1
SET,FIRST
/EFACET,1
PLNSOL,S,EQV,0,1.0

!Element stress
SELTOL,1E-6
NSEL,S,LOC,Y,0.4225/2
NSEL,R,LOC,X,0
NSORT,S,EQV,,1
*GET,Maxstress,SORT,,MAX
NSEL,ALL

FINISH
