! =================================================!
! ANSYS APDL-scripts - Simple Bone
!     - Bone
!    -----------------------------------------
!       Geometry: [m]
!       Force:    [N]
!       Moment:   [Nm]
!       Pressure: [Pa]
! =================================================!

FINISH		 ! Make sure current ANSYS module is closed
/CLEAR,START ! Clear the ANSYS workspace 

! White background
/RGB,INDEX,100,100,100, 0   
/RGB,INDEX, 80, 80, 80,13   
/RGB,INDEX, 60, 60, 60,14   
/RGB,INDEX, 0, 0, 0,15  

! =================================================!
! INPUT DATA - including material
! =================================================!

PI = 4*ATAN(1.0) ! Define variable PI as 3.1415...

! Remember to update title
*dim,TITLE_TEXT,string,20 ! Maximum 20 characters
/TITLE,%TITLE_TEXT(1)% Project 2

YOUNG_COMPACT = 19e9        !E.g. steel: 200 GPa = 200e3 MPa = 200e9 Pa [unit]
YOUNG_SPONGY = 10e9
YOUNG_MARROW = 0.001e9
POISSON_COMPACT = 0.4         !E.g. steel: 0.3 [-]
POISSON_SPONGY = 0.2
POISSON_MARROW = 0.3
TIMO = 1000*YOUNG_COMPACT

TH=0.035

! =================================================!
! GEOMETRY DEFINITIONS - in preprocessor (/PREP7)
! =================================================!
/PREP7 ! Enter the preprocessor

!Keypoints
K,1,0,0,0
K,2,0.035,0,0
K,3,0.035,0.440,0
K,4,-0.045,0.440,0
K,5,-0.045,0.4225,0
K,6,-0.045,0.405,0
K,7,0,0.405,0

!Lines
L,1,2
L,2,3
L,3,4
L,4,5
L,5,6
L,6,7
L,7,1

AL,all      ! Area from all lines

*ASK,TYPE,Select elementtype: 1=>Plane182 (Q4) 2=>Plane182 (Q6) 3=>Plane183 (Q8)
! =================================================!
! MATERIAL DEFINITIONS
! =================================================! 

MP,EX,1,YOUNG_COMPACT
MP,PRXY,1,POISSON_COMPACT
!MP,EX,2,YOUNG_SPONGY
!MP,PRXY,2,POISSON_SPONGY
!MP,EX,3,YOUNG_MARROW
!MP,PRXY,3,POISSON_MARROW

! =================================================!
! ELEMENT DEFINITIONS AND MESHING
! =================================================!

LESIZE,ALL,0.004,,,

*IF,TYPE,EQ,1,THEN
ET,1,PLANE182
KEYOPT,1,3,3
R,1,TH,

*ELSEIF,TYPE,EQ,2,THEN
ET,1,PLANE182
KEYOPT,1,3,3
R,1,TH,
KEYOPT,1,1,2

*ELSEIF,TYPE,EQ,3,THEN
ET,1,PLANE183
MSHAPE,0,2D
KEYOPT,1,3,3
R,1,TH
*ENDIF

TYPE,1
MAT,1
AMESH,ALL
! =================================================!
! DEFINE BOUNDARY CONDITIONS AND LOADS
! =================================================! 

NSEL,S,LOC,Y,0
D,ALL,,,,,,ALL
NSEL,ALL

FK,5,FX,72.57
FK,5,FY,-485.61

FINISH	! Exit the preprocessor

! =================================================!
! SOLUTION - in solution (/SOL)
! =================================================!

/SOL	! Enter the solution module


SOLVE	! Obtain solution

FINISH	! Exit the solution module


! =================================================!
! STUDY RESULTS - in postprocessor (/POST1) 
! =================================================!
/POST1
SET,FIRST



FINISH
